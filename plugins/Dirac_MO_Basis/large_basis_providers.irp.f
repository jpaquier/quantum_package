 BEGIN_PROVIDER [ double precision, large_ao_ortho_canonical_coef, (large_ao_num,large_ao_num)]
 &BEGIN_PROVIDER [ integer, large_ao_ortho_canonical_num ]
  implicit none
  BEGIN_DOC
 !Matrix of the coefficients of the large component MOs generated by the 
 ! orthonormalization by the S^{-1/2} canonical transformation of the large
 ! component AOs. 
 !large_ao_ortho_canonical_coef(i,j) = coefficient of the ith ao on the jth
 ! large_ao_ortho_canonical orbital
  END_DOC
  integer :: i
  large_ao_ortho_canonical_coef = 0.d0
  do i=1, large_ao_num
    large_ao_ortho_canonical_coef(i,i) = 1.d0
  enddo
  large_ao_ortho_canonical_num = large_ao_num
  call ortho_canonical(large_ao_overlap,size(large_ao_overlap,1), large_ao_num,large_ao_ortho_canonical_coef, size(large_ao_ortho_canonical_coef,1), large_ao_ortho_canonical_num)
 END_PROVIDER
  
 BEGIN_PROVIDER [double precision, large_ao_ortho_canonical_overlap, (large_ao_ortho_canonical_num, large_ao_ortho_canonical_num)]
  implicit none
  BEGIN_DOC
 !Overlap matrix of the large_ao_ortho_canonical.
 !Expected to be the Identity
  END_DOC
  integer                        :: i,j,k,l
  double precision               :: c
  do j=1, large_ao_ortho_canonical_num
    do i=1, large_ao_ortho_canonical_num
      large_ao_ortho_canonical_overlap(i,j) = 0.d0
    enddo
  enddo
  do j=1, large_ao_ortho_canonical_num
    do k=1, large_ao_num
      c = 0.d0
      do l=1, large_ao_num
        c +=  large_ao_ortho_canonical_coef(l,j) * large_ao_overlap(l,k)
      enddo
      do i=1, large_ao_ortho_canonical_num
        large_ao_ortho_canonical_overlap(i,j) += large_ao_ortho_canonical_coef(k,i) * c
      enddo
    enddo
  enddo
 END_PROVIDER

 BEGIN_PROVIDER [ integer, large_mo_tot_num ]
  implicit none
  BEGIN_DOC
  !Number of large component MOs
  END_DOC
  large_mo_tot_num = large_ao_ortho_canonical_num
  ASSERT (large_mo_tot_num > 0)
 END_PROVIDER


 BEGIN_PROVIDER [ double precision, large_mo_coef, (large_ao_num,large_mo_tot_num) ]
  implicit none
  BEGIN_DOC
  !Molecular orbital coefficients on large component AO basis set
  ! large_mo_coef(i,j) = coefficient of the ith large component ao on the jth
  ! large component mo
  END_DOC
  integer                        :: i, j
  double precision, allocatable  :: buffer(:,:)
  logical                        :: exists
  do i=1,large_mo_tot_num
    do j=1,large_ao_num
      large_mo_coef(j,i) = large_ao_ortho_canonical_coef(j,i)
    enddo
  enddo
 END_PROVIDER



 BEGIN_PROVIDER [double precision, large_ao_ortho_lowdin_coef, (large_ao_num,large_ao_num)]
  implicit none
  BEGIN_DOC
  !Matrix of the coefficients of the MOs generated by the Lowdin orthonormalization of the large component AOs
  ! large_ao_ortho_lowdin_coef(i,j) = coefficient of the ith ao on the jth large_ao_ortho_lowdin orbital
  END_DOC
  integer                        :: i,j,k,l
  double precision               :: accu
  double precision, allocatable  :: tmp_matrix(:,:)
  allocate (tmp_matrix(large_ao_num,large_ao_num))
  tmp_matrix(:,:) = 0.d0
  do j=1, large_ao_num
    tmp_matrix(j,j) = 1.d0
  enddo
  call ortho_lowdin(large_ao_overlap,large_ao_num,large_ao_num,tmp_matrix,large_ao_num,large_ao_num)
  do i=1, large_ao_num
    do j=1, large_ao_num
      large_ao_ortho_lowdin_coef(j,i) = tmp_matrix(i,j)
    enddo
  enddo
  deallocate(tmp_matrix)
 END_PROVIDER

 BEGIN_PROVIDER [double precision, large_ao_ortho_lowdin_overlap, (large_ao_num,large_ao_num)]
  implicit none
  BEGIN_DOC
  !Overlap matrix of the large_ao_ortho_lowdin
  ! supposed to be the Identity
  END_DOC
  integer                        :: i,j,k,l
  double precision               :: c
  do j=1, large_ao_num
    do i=1, large_ao_num
      large_ao_ortho_lowdin_overlap(i,j) = 0.d0
    enddo
  enddo
  do k=1, large_ao_num
    do j=1, large_ao_num
      c = 0.d0
      do l=1, large_ao_num
        c +=  large_ao_ortho_lowdin_coef(j,l) * large_ao_overlap(k,l)
      enddo
      do i=1, large_ao_num
        large_ao_ortho_lowdin_overlap(i,j) += large_ao_ortho_lowdin_coef(i,k) * c
      enddo
    enddo
  enddo
END_PROVIDER


